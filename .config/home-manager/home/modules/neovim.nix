{
  pkgs,
  lib,
  ...
}: let
  # with extraLuaPackages defined hm will try to add init.lua due, which conflicts with my init.lua
  # here we replicate the hm logic and generate a separate lua file that we will later import in init.lua
  x = pkgs.neovim-unwrapped.lua.pkgs;
  luaPackages = with x; [
    jsregexp # for luasnip
    toml-edit # toml parser
    lyaml # yaml parser
  ];

  generatedLuaPath = lib.concatMapStringsSep ";" x.getLuaPath luaPackages;
  generatedLuaCPath = lib.concatMapStringsSep ";" x.getLuaCPath luaPackages;

  luaPackagesFile = ''
    -- generated by home-manager

    -- lua package paths
    package.path = "${generatedLuaPath}" .. ";" .. package.path
    package.cpath = "${generatedLuaCPath}" .. ";" .. package.cpath

    -- required for copilot
    vim.g.copilot_node_command = "${pkgs.nodejs_24}/bin/node"
  '';
in {
  programs.neovim = {
    enable = true;
    extraPackages = with pkgs;
      [
        parinfer-rust
        tree-sitter # to build grammars from source
        nodejs_24

        lua-language-server # A language server for Lua
        nil #nix language server
        nixd # nix language server
        clojure-lsp
        slint-lsp
        rust-analyzer # rust language server
        dockerfile-language-server # docker language server
        basedpyright # python language server

        alejandra # formatting nix
        shfmt # formatting shell scripts
        rustfmt # formatting rust

        golines # formatting long lines in go
        templ # formatting go templ files
        gopls # Go language server
      ]
      ++ (with nodePackages; [
        yaml-language-server # A language server for YAML
        bash-language-server # A language server for Bash
        typescript-language-server # TypeScript & JavaScript Language Server
        vscode-langservers-extracted # Language servers extracted from VSCode, css mainly
        tailwindcss-language-server # Language server for Tailwind CSS
        vim-language-server # Language server for Vim script

        prettier # for formatting js, ts, css, html, json, yaml, markdown
        # purs-tidy # formatting purescript (not in nixpkgs anymore)
        sql-formatter # formatting sql
      ]);

    plugins = with pkgs.vimPlugins; [
      parinfer-rust
      aniseed
      lazy-nvim
    ];
  };

  # we generate a separate file for the cpath/path
  # this must be imported by the init.lua file
  xdg.configFile."nvim/lua/home-manager.lua".text = luaPackagesFile;
}
