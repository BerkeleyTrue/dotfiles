#!/usr/bin/env bb
(ns local.bin.multicoretemp
  (:require [clojure.string :as str]
            [clojure.math :as math]
            [babashka.fs :as fs]
            [cheshire.core :as json]))

(defn tap [fn x] (fn x) x)

(defn find-coretemp-path []
  (->> (fs/glob "/sys/bus/platform/devices/" "coretemp.*")
       (filter #(fs/directory? %))
       (map str)
       (first)))

(comment
  (fs/glob "/sys/bus/platform/devices/" "coretemp.*")
  (fs/glob "/sys/bus/platform/devices/coretemp.0/" "hwmon/hwmon*")
  (->> (fs/list-dir "/sys/bus/platform/devices/coretemp.0/hwmon")
       (filter #(str/starts-with? (str %) "/sys/bus/platform/devices/coretemp.0/hwmon/hwmon")))
  (find-coretemp-path))

(defn get-hwmon-paths []
  (let [coretemp-path (find-coretemp-path)]
    (if coretemp-path
      (->> (fs/list-dir (str coretemp-path "/hwmon"))
           (map str)
           (filter #(str/starts-with? (str %) (str coretemp-path "/hwmon/hwmon")))
           (map str))
      ; fallback to /sys/class/hwmon
      (->> (fs/glob "/sys/class/hwmon/" "hwmon*")
           (filter fs/directory?)
           (map str)))))

(comment
  (get-hwmon-paths))

(defn is-core-label?
  "Check if a label file refers to a cpu core"
  [path]
  (let [label (->> (slurp path)
                   (take 4)
                   (str/join ""))]
                ; Core is intel per core temp
                ; Tdie is amd control temp
                ; Tctl is die temp
    (contains? #{"Core" "Tdie" "Tctl"} label)))

(comment
  (take 4 (slurp "/sys/class/hwmon/hwmon2/temp1_label"))
  (is-core-label? "/sys/class/hwmon/hwmon2/temp1_label")
  (->> (fs/glob "/sys/class/hwmon/hwmon2/" "temp*_label" {:follow-links true})
       (map str)
       (filter fs/exists?)
       (filter is-core-label?)))

(defn get-core-paths
  "Get all core temperature input file paths"
  []
  (->> (get-hwmon-paths)
       (map (fn [hwmon-path]
              (->> (fs/glob hwmon-path "temp*_label" {:follow-links true})
                   (map str)
                   (filter fs/exists?)
                   (filter is-core-label?)
                   (map #(str/replace % "_label" "_input"))
                   (filter fs/exists?))))
       (apply concat)))

(comment
  (get-core-paths))

(defn read-core-temps
  "Read all core temperatures and return as a sequence of numbers in millidegrees"
  []
  (->> (get-core-paths)
       (map (fn [path]
              (-> (slurp path)
                  (str/trim)
                  (parse-long)
                  (or 0))))))

(comment
  (parse-long (str/trim " 42000"))
  (read-core-temps))

(defn average-core-temp
  "Return the average core temperature in degrees Celsius"
  []
  (let [temps (read-core-temps)
        avg-millideg (if (seq temps)
                       (math/round (/ (reduce + temps)
                                      (count temps)))
                       0)]
    (-> avg-millideg
        (/ 1000.0)
        (math/round))))

(comment
  (average-core-temp))

(defn get-icon [temp]
  (cond
    (< temp 50) "<span foreground='#85c1dc'></span>"
    (< temp 75) "<span foreground='#ef9f76'></span>"
    :else "<span foreground='#ea999c'></span>"))

(comment
  (get-icon 45)
  (get-icon 65)
  (get-icon 85))

(defn -main []
  (let [avg-temp (average-core-temp)
        icon (get-icon avg-temp)]
    (println (json/generate-string {:text (str icon " " avg-temp "°C")}))))

(when (= *file* (System/getProperty "babashka.file"))
  (-main))
